import org.apache.tools.ant.taskdefs.condition.Os

plugins {
    id 'org.springframework.boot' version '2.4.2'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'
}

group = 'ee.adamson'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '14'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.apache.commons:commons-lang3'

    compileOnly 'org.projectlombok:lombok'

    annotationProcessor 'org.projectlombok:lombok'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

test {
    useJUnitPlatform()
}

task buildUI(type: CommonExec) {
    workingDir new File(project.projectDir, "frontend/arbiter-app")
    arguments = ['npm', 'run', 'install-build']
}

task copyUI(type: Copy) {
    from "frontend/arbiter-app/dist/arbiter-app/"
    into "src/main/resources/static/"
}

task buildApp(dependsOn: ['buildUI', 'copyUI']) {
}

build.dependsOn buildApp


class CommonExec extends Exec {

    public void setArguments(List arguments) {
        def commands = Os.isFamily(Os.FAMILY_WINDOWS) ? ['cmd', '/c'] : []
        commands = commands + arguments
        this.setCommandLine(commands)
    }
}
